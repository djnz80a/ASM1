;------
C.ADRS:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	A,H
	AND	L
	INC	A
	JR	Z,C.ADRS.1
	LD	HL,(VAL1)
	LD	(O.ADR1),HL
	JP	MAIN20

C.ADRS.1:
	LD	HL,(ORG.ADR)
	LD	DE,(O.BYTE)
	ADD	HL,DE
	LD	(O.ADR1),HL
	JP	MAIN20

;
;
;
C.ORG:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	(ORG.ADR),HL
	LD	(O.ADR1),HL
	JP	MAIN20
;
;
;
C.END:	
	DEC	IX
	CALL	S.SKIP
	JP	NZ,ERRB
	JP	MAIN02	;RET
;
;
;
C.DB:	
	DEC	IX
	CALL	S.SKIP
C.DB0:	
	LD	A,(IX+0)
	CP	27H
	JR	Z,C.DB3
	CP	'"'
	JR	Z,C.DB3
C.DB1:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,L
	CALL	SETDAT
	DEC	IX
	CALL	S.SKIP
	LD	A,(IX+0)
	CP	','
	JP	NZ,MAIN20
	INC	IX
	JR	C.DB0
C.DB3:	LD	C,A
	LD	A,(IX+2)
	CP	C
	JR	Z,C.DB1
C.DB4:
	INC	IX
	LD	A,(IX+0)
	CP	C
	JR	Z,C.DB6
	CP	7FH
	JP	Z,ERR6
	PUSH	BC
	CALL	SETDAT
	POP	BC
	JP	C.DB4
C.DB6:	
	CALL	S.SKIP
	LD	A,(IX+0)
	CP	','
	JP	NZ,MAIN20
	INC	IX
	JR	C.DB0
;
;
;
C.DW:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
C.DW1:	LD	HL,(VAL1)
	CALL	SETD2
	DEC	IX
	CALL	S.SKIP
	JP	Z,MAIN20
	CALL	OPCOD2
	JR	C.DW1
;
;
;
C.DWR:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
C.DWR1:	LD	HL,(VAL1)
	PUSH	HL
	LD	A,H
	CALL	SETDAT
	POP	HL
	LD	A,L
	CALL	SETDAT
	DEC	IX
	CALL	S.SKIP
	JP	Z,MAIN20
	CALL	OPCOD2
	JR	C.DWR1
;
;
;
C.DM:	
	DEC	IX
	CALL	S.SKIP
	LD	A,(IX+0)
	CP	27H
	JR	Z,C.DM1-1
	CP	'"'
	JP	NZ,ERRB
	LD	C,A
C.DM1:	INC	IX
	LD	A,(IX+0)
	CP	C
	JP	Z,C.DM2
	PUSH	BC
	CALL	SETDAT
	POP	BC
	JR	C.DM1
C.DM2:	INC	IX
	JP	MAIN20
;
;
;
C.DS:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	DEC	IX
	CALL	S.SKIP
	LD	A,(IX+0)
	CP	','
	LD	B,00H
	LD	HL,(VAL1)
	LD	(VAL2),HL
	JR	NZ,C.DS1
	CALL	OPCOD2
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	B,L
C.DS1:	LD	HL,(VAL2)
C.DS2:	LD	A,H
	OR	L
	JP	Z,MAIN20
	LD	A,B
	PUSH	BC
	CALL	SETDAT
	POP	BC
	DEC	HL
	JR	C.DS2
;
;
;
C.ADC:	CALL	OPCODE
	CP	12H
	JP	Z,C.ADC2
	CP	6
	JP	NZ,ERRB
	CALL	OPCOD2
	CP	7
	JR	C,C.AC10
	CP	50H
	JR	Z,C.AC20
	CP	32H
	JR	Z,C.AC30
	CP	40H
	JR	Z,C.AC40
	CP	41H
	JR	Z,C.AC50
	JP	ERRB
C.AC10:	CP	6	;adc a,r
	JR	NZ,$+3
	INC	A
	OR	88H
	CALL	SETDAT
	JP	MAIN20
C.AC20:	LD	A,0CEH	;adc a,n
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.AC30:	LD	A,8EH	;adc a,(hl)
	JP	SETD1
C.AC40:	LD	A,0DDH	;adc a,(ix+d)
	CALL	SETDAT
	LD	A,8EH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.AC50:	LD	A,0FDH	;adc a,(iy+d)
	CALL	SETDAT
	LD	A,8EH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1

C.ADC2:	CALL	OPCOD2
	CP	16H
	JR	Z,C.ADC3
	CP	13H
	JP	NC,ERRB
	JR	C.ADC4
C.ADC3:	LD	A,13H
C.ADC4:	SUB	10H
	RLCA
	RLCA
	RLCA
	RLCA
	OR	4AH
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	JP	SETD1
;
;
;
C.ADD:	CALL	OPCODE
	CP	12H
	JP	Z,C.ADD2
	CP	14H
	JP	Z,C.ADD5
	CP	15H
	JP	Z,C.ADD6
	CP	6
	JP	NZ,ERRB
C.AD10:	CALL	OPCOD2
	CP	50H
	JP	Z,C.AD20
	CP	32H
	JR	Z,C.AD30
	CP	40H
	JR	Z,C.AD40
	CP	41H
	JR	Z,C.AD50
	CP	6	;add a,r
	JR	NZ,$+3
	INC	A
	OR	80H
	JP	SETD1
C.AD20:	LD	A,0C6H	;add a,n
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.AD30:	LD	A,86H	;add a,(hl)
	JP	SETD1
C.AD40:	LD	A,0DDH	;add a,(ix+d)
	CALL	SETDAT
	LD	A,86H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.AD50:	LD	A,0FDH	;add a,(iy+d)
	CALL	SETDAT
	LD	A,86H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1

C.ADD2:	CALL	OPCOD2
	CP	16H
	JR	Z,C.ADD3
	CP	13H
	JP	NC,ERRB
	JR	C.ADD4
C.ADD3:	LD	A,13H
C.ADD4:	SUB	10H
	RLCA
	RLCA
	RLCA
	RLCA
	OR	09H
	JP	SETD1

C.ADD5:	CALL	OPCOD2
	CP	10H
	LD	B,0
	JR	Z,C.AD51
	CP	11H
	LD	B,1
	JR	Z,C.AD51
	CP	14H
	LD	B,2
	JR	Z,C.AD51
	CP	16H
	LD	B,3
	JR	Z,C.AD51
	JP	ERRB
C.AD51:	PUSH	BC
	LD	A,0DDH
	CALL	SETDAT
	POP	BC
	LD	A,B
	RLCA
	RLCA
	RLCA
	RLCA
	OR	09H
	JP	SETD1

C.ADD6:	CALL	OPCOD2
	CP	10H
	LD	B,0
	JR	Z,C.AD61
	CP	11H
	LD	B,1
	JR	Z,C.AD61
	CP	15H
	LD	B,2
	JR	Z,C.AD61
	CP	16H
	LD	B,3
	JR	Z,C.AD61
	JP	ERRB
C.AD61:	PUSH	BC
	LD	A,0FDH
	CALL	SETDAT
	POP	BC
	LD	A,B
	RLCA
	RLCA
	RLCA
	RLCA
	OR	09H
	JP	SETD1
;
;
;
C.AND:	LD	C,0A0H
	JR	C1
C.CP:	LD	C,0B8H
	JR	C1
C.OR:	LD	C,0B0H
	JR	C1
C.SUB:	LD	C,90H
	JR	C1
C.XOR:	LD	C,0A8H
	JR	C1
;
;
;
C1:	PUSH	BC
	CALL	OPCODE
	CP	50H
	JP	Z,C1.20
	CP	32H
	JR	Z,C1.30
	CP	40H
	JR	Z,C1.40
	CP	41H
	JR	Z,C1.50
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	POP	BC
	OR	C
	JP	SETD1
C1.20:	POP	BC
	LD	A,C
	OR	46H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C1.30:	POP	BC
	LD	A,C
	OR	06H
	JP	SETD1
C1.40:	LD	A,0DDH
	CALL	SETDAT
	POP	BC
	LD	A,C
	OR	06H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C1.50:	LD	A,0FDH
	CALL	SETDAT
	POP	BC
	LD	A,C
	OR	06H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
;
;
;
C.BIT:	LD	C,40H
	JR	C2
C.RES:	LD	C,80H
	JR	C2
C.SET:	LD	C,0C0H
	JR	C2
;
;
;
C2:	PUSH	BC
	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	A,L
	PUSH	AF
	CALL	OPCOD2
	CP	32H
	JR	Z,C2.4		;(HL)
	CP	40H
	JR	Z,C2.5		;(IX+
	CP	41H
	JR	Z,C2.6		;(IY+
	CP	7
	JR	C,C2.2
	JP	ERRB

C2.2:	CP	6
	JR	NZ,$+3
	INC	A
C2.3:	LD	C,A
	POP	AF
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OR	C
	POP	BC
	OR	C
	PUSH	AF
	LD	A,0CBH
	CALL	SETDAT
	POP	AF
	JP	SETD1
C2.4:	LD	A,6
	JR	C2.3


C2.5:	LD	A,0DDH

C2.55:	CALL	SETDAT
	LD	A,0CBH
	CALL	SETDAT
	LD	A,(VAL1)
	CALL	SETDAT
	POP	AF
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,06H
	POP	BC
	OR	C
	JP	SETD1
C2.6:	LD	A,0FDH
	JR	C2.55


;
C.CALL:	CALL	OPCODE
	CP	50H
	JR	NZ,C.CAL2
	LD	A,0CDH
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.CAL2:	CP	1
	JR	NZ,$+4
	LD	A,23H
	CP	20H
	JP	C,ERRB
	CP	28H
	JP	NC,ERRB
	SUB	20H
	RLCA
	RLCA
	RLCA
	OR	0C4H
	CALL	SETDAT
	CALL	OPCOD2
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20
;
;
;
C.DJNZ:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	A,(PASS)
	DEC	A
	LD	L,00H
	JR	Z,C.DJ10
	LD	HL,(VAL1)
	LD	DE,80H
	ADD	HL,DE
	LD	DE,(O.ADR1)
	INC	DE
	INC	DE
	OR	A
	SBC	HL,DE
	JP	C,ERRA
	LD	A,H
	OR	A
	JP	NZ,ERRA
	LD	A,L
	ADD	A,80H
C.DJ10:	PUSH	AF
	LD	A,10H
	CALL	SETDAT
	POP	AF
	JP	SETD1
;
;
;
C.EX:	CALL	OPCODE
	CP	11H	;de,
	JR	Z,C.EX10
	CP	13H	;af,
	JR	Z,C.EX20
	CP	33H	;(sp)
	JP	NZ,ERRB
	CALL	OPCOD2
	CP	12H	;(sp),hl
	JR	Z,C.EX08
	CP	14H	;(sp),ix
	LD	B,0DDH
	JR	Z,C.EX05
	CP	15H	;(sp),iy
	LD	B,0FDH
	JR	Z,C.EX05
	JP	ERRB
C.EX05:	LD	A,B
	CALL	SETDAT
C.EX08:	LD	A,0E3H
	JP	SETD1

C.EX10:	CALL	OPCOD2
	CP	12H	;de,hl
	JP	NZ,ERRB
	LD	A,0EBH
	JP	SETD1

C.EX20:	LD	DE,ERRB
	PUSH	DE	;af,af'
	LD	A,(IX+0)
	INC	IX
	CP	','
	RET	NZ
	LD	A,(IX+0)
	CALL	UPPER
	INC	IX
	CP	'A'
	RET	NZ
	LD	A,(IX+0)
	CALL	UPPER
	INC	IX
	CP	'F'
	RET	NZ
	LD	A,(IX+0)
	CP	27H
	RET	NZ
	INC	IX
	RET	NZ
	POP	DE
	LD	A,08H
	JP	SETD1
;
;
;
C.IM:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	A,L
	CP	3
	JP	NC,ERR7
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	OR	A
	JR	NZ,C.IM1
	LD	A,46H
	JP	SETD1
C.IM1:	DEC	A
	JR	NZ,C.IM2
	LD	A,56H
	JP	SETD1
C.IM2:	LD	A,5EH
	JP	SETD1
;
;
;
C.IN:	CALL	OPCODE
	LD	B,A
	PUSH	BC
	CALL	OPCOD2
	POP	BC
	LD	C,A
	CP	42H
	JR	Z,C.IN10
	CP	50H
	JR	Z,C.IN10
	CP	34H
	JR	Z,C.IN20
	JP	ERRB
C.IN10:	LD	A,B
	CP	6
	JP	NZ,ERRB
	LD	A,0DBH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.IN20:	LD	A,B
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	OR	40H
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	JP	SETD1
;
;
;
C.INC:	LD	C,04H
	JR	C3
C.DEC:	LD	C,05H
	JR	C3
;
;
;
C3:	PUSH	BC
	CALL	OPCODE
	CP	7
	JR	C,C3.10
	CP	32H
	JR	Z,C3.20
	CP	14H
	JR	Z,C3.50
	CP	15H
	JR	Z,C3.60
	CP	40H
	JR	Z,C3.30
	CP	41H
	JR	Z,C3.40
	CP	10H
	JP	C,ERRB
	CP	14H
	JP	Z,C3.80
	CP	15H
	JP	Z,C3.90
	CP	16H
	JR	NZ,$+4
	LD	A,13H
	SUB	10H
	CP	4
	JP	NC,ERRB
	JP	C3.70
C3.10:	CP	6
	JR	NZ,$+3
	INC	A
C3.15:	RLCA
	RLCA
	RLCA
	POP	BC
	OR	C
	JP	SETD1
C3.20:	LD	A,6
	JR	C3.15
C3.30:	PUSH	AF
	LD	A,0DDH
C3.35:	CALL	SETDAT
	POP	AF
	POP	BC
	LD	A,C
	OR	30H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C3.40:	PUSH	AF
	LD	A,0FDH
	JR	C3.35

C3.50:	LD	A,0DDH
C3.55:	CALL	SETDAT
	POP	BC
	LD	A,C
	RLCA
	RLCA
	RLCA
	OR	03H
	JP	SETD1

C3.60:	LD	A,0FDH
	JR	C3.55

C3.70:	RLCA
	RLCA
	RLCA
	RLCA
	OR	03H
	POP	BC
	LD	B,A
	LD	A,C
	AND	1
	RLCA
	RLCA
	RLCA
	OR	B
	JP	SETD1

C3.80:	LD	A,0DDH
C3.85:	CALL	SETDAT
	POP	BC
	AND	1
	RLCA
	RLCA
	RLCA
	OR	23H
	JP	SETD1
C3.90:	LD	A,0FDH
	JR	C3.85
;
;
;
C.JP:	CALL	OPCODE
	CP	50H
	JR	NZ,C.JP2
	LD	A,0C3H
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.JP2:	CP	1
	JR	NZ,$+4
	LD	A,23H
	CP	32H
	JR	Z,C.JP10
	CP	35H
	JR	Z,C.JP20
	CP	36H
	JR	Z,C.JP30
	CP	20H
	JP	C,ERRB
	CP	28H
	JP	NC,ERRB
	SUB	20H
	RLCA
	RLCA
	RLCA
	OR	0C2H
	PUSH	AF
	CALL	OPCOD2
	CP	50H
	JP	NZ,ERRB
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20
C.JP10:	LD	A,0E9H
	JP	SETD1
C.JP20:	LD	A,0DDH
	CALL	SETDAT
	JR	C.JP10
C.JP30:	LD	A,0FDH
	CALL	SETDAT
	JR	C.JP10
;
;
;
C.JR:	CALL	OPCODE
	CP	50H
	JR	Z,C.JR10
	CP	1
	JR	NZ,$+4
	LD	A,23H
	CP	20H
	JP	C,ERRB
	CP	24H
	JP	NC,ERRB
	SUB	20H
	RLCA
	RLCA
	RLCA
	OR	20H
	PUSH	AF
	CALL	OPCOD2
	CP	50H
	JP	NZ,ERRB
C.JR15:	LD	A,(PASS)
	DEC	A
	LD	L,00H
	JR	Z,C.JR2
	LD	HL,(VAL1)
	LD	DE,80H
	ADD	HL,DE
	LD	DE,(O.ADR1)
	INC	DE
	INC	DE
	OR	A
	SBC	HL,DE
	JP	C,ERRA
	LD	A,H
	OR	A
	JP	NZ,ERRA
	LD	A,L
	ADD	A,80H
	LD	L,A
C.JR2:	POP	AF
	PUSH	HL
	CALL	SETDAT
	POP	HL
	LD	A,L
	JP	SETD1
C.JR10:	LD	A,18H
	PUSH	AF
	JR	C.JR15
;
;
;
C.LD:	CALL	OPCODE
	LD	B,A
	PUSH	BC
	LD	HL,(VAL1)
	PUSH	HL
	CALL	OPCOD2
	LD	HL,(VAL1)
	LD	(VAL2),HL
	POP	HL
	LD	(VAL1),HL
	POP	BC
	LD	C,A
	LD	A,B
	CP	32H	;(hl),
	JP	Z,C.L10
	CP	40H	;(ix+d),
	JP	Z,C.L20
	CP	41H	;(iy+d),
	JP	Z,C.L30
	CP	30H	;(bc),
	JP	Z,C.L40
	CP	31H	;(de),
	JP	Z,C.L50
	CP	42H	;(nn),
	JP	Z,C.L60
	CP	9	;b,c,d,e,h,l,a,i,r
	JP	C,C.L70
	CP	10H
	JP	C,ERRB
	CP	17H
	JP	NC,ERRB

C.L00:	CP	14H	;ix
	JP	Z,C.L05
	CP	15H	;iy
	JP	Z,C.L06
	CP	13H	;af
	JP	Z,ERRB
	CP	16H	;sp(16h)=>(13h)
	JR	NZ,$+4
	LD	A,13H

	SUB	10H
	LD	D,A
	CP	3	;?sp
	JP	NZ,C.L01
	LD	A,C
	CP	42H	;sp,(nn)
	JP	Z,C.L01
	CP	50H	;sp,nn
	JP	Z,C.L01
	CP	12H	;sp,hl
	JR	Z,C.L002
	CP	14H	;sp,ix
	LD	B,0DDH
	JR	Z,C.L001
	CP	15H	;sp,iy
	LD	B,0FDH
	JR	Z,C.L001
	JP	ERRB
C.L001:LD	A,B
	CALL	SETDAT
C.L002:	LD	A,0F9H
	JP	SETD1



C.L01:	LD	A,C
	CP	50H
	JP	NZ,C.L02
	LD	A,D	;ld { bc,de,hl },nn
	RLCA
	RLCA
	RLCA
	RLCA
	OR	01H
	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L02:	CP	42H
	JP	NZ,ERRB
	LD	A,D
	CP	2
	JR	NZ,C.L03
	LD	A,2AH	;ld hl,(nn)
	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L03:	RLCA	;ld { bc,de },(nn)
	RLCA
	RLCA
	RLCA
	OR	4BH
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L05:	LD	A,C
	CP	42H
	JR	Z,C.L055
	CP	50H
	JP	NZ,ERRB
	LD	A,0DDH	;ix,nn
	CALL	SETDAT
	LD	A,21H
C.L050:	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L055:	LD	A,0DDH	;ix,(nn)
	CALL	SETDAT
	LD	A,2AH
	JR	C.L050

C.L06:	LD	A,C
	CP	42H
	JR	Z,C.L065
	CP	50H
	JP	NZ,ERRB
	LD	A,0FDH	;iy,nn
	CALL	SETDAT
	LD	A,21H
C.L060:	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L065:	LD	A,0FDH	;iy,(nn)
	CALL	SETDAT
	LD	A,2AH
	JR	C.L060

C.L10:	LD	A,C
	CP	50H
	JR	Z,C.L15
	CP	7	;(hl),r
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	OR	70H
	JP	SETD1

C.L15:	LD	A,36H	;(hl),n
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1

C.L20:	LD	A,C
	CP	50H
	JR	Z,C.L25
	CP	7	;(ix+d),r
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	OR	70H
	PUSH	AF
	LD	A,0DDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	A,L
	JP	SETD1

C.L25:	LD	A,0DDH	;(ix+d),n
	CALL	SETDAT
	LD	A,36H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1

C.L30:	LD	A,C
	CP	50H
	JR	Z,C.L35
	CP	7	;(iy+d),n
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	OR	70H
	PUSH	AF
	LD	A,0FDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1

C.L35:	LD	A,0FDH	;(iy+d),n
	CALL	SETDAT
	LD	A,36H
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1

C.L40:	LD	A,C	;ld (bc),a
	CP	6
	JP	NZ,ERRB
	LD	A,02H
	JP	SETD1

C.L50:	LD	A,C	;ld (de),a
	CP	6
	JP	NZ,ERRB
	LD	A,12H
	JP	SETD1

C.L60:	LD	A,C
	CP	6	;(nn),a
	JR	Z,C.L61
	CP	14H	;(nn),ix
	JP	Z,C.L65
	CP	15H	;(nn),iy
	JP	Z,C.L66
	LD	A,C	;(nn),dd
	CP	13H
	JP	Z,ERRB
	CP	16H
	JR	NZ,$+4
	LD	A,13H
	CP	10H
	JP	C,ERRB
	CP	14H
	JP	NC,ERRB
	SUB	10H
	CP	2
	JR	NZ,C.L602
	LD	A,22H	;(nn),hl
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.L602:	RLCA		;(nn),dd
	RLCA
	RLCA
	RLCA
	OR	43H
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.L61:	LD	A,32H	;(nn),a
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.L65:	LD	A,0DDH	;ld (nn),ix
	CALL	SETDAT
	LD	A,22H
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.L66:	LD	A,0FDH	;ld (nn),iy
	CALL	SETDAT
	LD	A,22H
	CALL	SETDAT
	LD	HL,(VAL1)
	CALL	SETD2
	JP	MAIN20

C.L70:	LD	A,B
	CP	7	;i,
	JP	Z,C.L71
	CP	8	;r,
	JP	Z,C.L72
	LD	A,C
	CP	7	;,i
	JP	Z,C.L73
	CP	8	;,r
	JP	Z,C.L74
	CP	30H	;(bc)
	JP	Z,C.L75
	CP	31H	;(de)
	JP	Z,C.L76
	CP	32H	;(hl)
	JP	Z,C.L77
	CP	42H	;(nn)
	JP	Z,C.L78
	CP	40H	;(iy+d)
	JP	Z,C.L79
	CP	41H	;(ix+d)
	JP	Z,C.L7A
	CP	50H	;n
	JP	Z,C.L7B
	LD	A,B	;ld r,r'
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	LD	D,A
	LD	A,C
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	OR	D
	OR	40H
	JP	SETD1

C.L71:	LD	A,C	;ld i,a
	CP	6
	JP	NZ,ERRB
	LD	A,0EDH
	CALL	SETDAT
	LD	A,47H
	JP	SETD1

C.L72:	LD	A,C	;ld r,a
	CP	6
	JP	NZ,ERRB
	LD	A,0EDH
	CALL	SETDAT
	LD	A,4FH
	JP	SETD1

C.L73:	LD	A,B	;ld a,i
	CP	6
	JP	NZ,ERRB
	LD	A,0EDH
	CALL	SETDAT
	LD	A,57H
	JP	SETD1

C.L74:	LD	A,B	;ld a,r
	CP	6
	JP	NZ,ERRB
	LD	A,0EDH
	CALL	SETDAT
	LD	A,5FH
	JP	SETD1

C.L75:	LD	A,B	;ld a,(bc)
	CP	6
	JP	NZ,ERRB
	LD	A,0AH
	JP	SETD1

C.L76:	LD	A,B	;ld a,(de)
	CP	6
	JP	NZ,ERRB
	LD	A,1AH
	JP	SETD1

C.L77:	LD	A,B	;ld r,(hl)
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	OR	46H
	JP	SETD1

C.L78:	LD	A,B	;ld a,(nn)
	CP	6
	JP	NZ,ERRB
	LD	A,3AH
	CALL	SETDAT
	LD	HL,(VAL2)
	CALL	SETD2
	JP	MAIN20

C.L79:	LD	A,B	;ld r,(ix+d)
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	OR	46H
	PUSH	AF
	LD	A,0DDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1

C.L7A:	LD	A,B
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	OR	46H
	PUSH	AF
	LD	A,0FDH
	CALL	SETDAT
	POP	AF
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1

C.L7B:	LD	A,B
	CP	6
	JR	NZ,$+3
	INC	A
	RLCA
	RLCA
	RLCA
	OR	06H
	CALL	SETDAT
	LD	HL,(VAL2)
	LD	A,L
	JP	SETD1
;
;
;
C.OUT:	CALL	OPCODE
	CP	34H
	JR	Z,C.OUT2
	CP	42H
	JP	Z,C.OUT1
	CP	50H
	JP	Z,C.OUT1
	JP	ERRB
C.OUT1:	CALL	OPCOD2
	CP	6
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	A,L
	PUSH	AF
	LD	A,0D3H
	CALL	SETDAT
	POP	AF
	JP	SETD1
C.OUT2:	CALL	OPCOD2
	CP	7
	JP	NC,ERRB
	CP	6
	JR	NZ,$+3
	INC	A
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	RLCA
	RLCA
	RLCA
	OR	41H
	JP	SETD1
;
;
;
C.POP:	CALL	OPCODE
	SUB	10H
	JP	C,ERRB
	CP	6
	JP	NC,ERRB
	CP	4
	JR	NC,POP3
POP2:	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,0C1H
	JR	POP5
POP3:	CP	4
	LD	A,0DDH
	JR	Z,POP4
	LD	A,0FDH
POP4:	CALL	SETDAT
	LD	A,0E1H
POP5:	JP	SETD1
;
;
;
C.PUSH:	CALL	OPCODE
	SUB	10H
	JP	C,ERRB
	CP	6
	JP	NC,ERRB
	CP	4
	JR	NC,PUSH3
PUSH2:	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,0C5H
	JR	PUSH5
PUSH3:	CP	4
	LD	A,0DDH
	JR	Z,PUSH4
	LD	A,0FDH
PUSH4:	CALL	SETDAT
	LD	A,0E5H
PUSH5:	JP	SETD1
;
;
;
C.RET:	
	DEC	IX
	CALL	S.SKIP
	JR	NZ,C.RET2
	LD	A,0C9H
	JP	SETD1
C.RET2:	CALL	OPCODE
	CP	1
	JR	NZ,$+4
	LD	A,23H
	CP	20H
	JP	C,ERRB
	CP	28H
	JP	NC,ERRB
	SUB	20H
	RLCA
	RLCA
	RLCA
	OR	0C0H
	JP	SETD1
;
;
;
C.RST:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	HL,(VAL1)
	LD	A,H
	OR	A
	JP	NZ,ERR7
	LD	A,L
	CP	39H
	JP	NC,ERR7
	AND	7
	JP	NZ,ERR7
	LD	A,L
	AND	38H
	OR	0C7H
	JP	SETD1
;
;
;
C.SBC:	CALL	OPCODE
	CP	12H
	JP	Z,C.SBC2
	CP	6
	JP	NZ,ERRB
	CALL	OPCOD2
	CP	7
	JR	C,C.SC10
	CP	50H
	JR	Z,C.SC20
	CP	32H
	JR	Z,C.SC30
	CP	40H
	JR	Z,C.SC40
	CP	41H
	JR	Z,C.SC50
	JP	ERRB
C.SC10:	CP	6	;sbc a,r
	JR	NZ,$+3
	INC	A
	OR	98H
	JP	SETD1
C.SC20:	LD	A,0DEH	;sbc a,n
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.SC30:	LD	A,9EH	;sbc a,(hl)
	JP	SETD1
C.SC40:	LD	A,0DDH	;sbc a,(ix+d)
	CALL	SETDAT
	LD	A,9EH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1
C.SC50:	LD	A,0FDH	;sbc a,(iy+d)
	CALL	SETDAT
	LD	A,9EH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	JP	SETD1

C.SBC2:	CALL	OPCOD2
	CP	16H
	JR	Z,C.SBC3
	CP	13H
	JP	NC,ERRB
	JR	C.SBC4
C.SBC3:	LD	A,13H
C.SBC4:	SUB	10H
	RLCA
	RLCA
	RLCA
	RLCA
	OR	42H
	PUSH	AF
	LD	A,0EDH
	CALL	SETDAT
	POP	AF
	JP	SETD1
;
;
;
C.RLC:	LD	C,00H
	JR	C4
C.RRC:	LD	C,08H
	JR	C4
C.RL:	LD	C,10H
	JR	C4
C.RR:	LD	C,18H
	JR	C4
C.SLA:	LD	C,20H
	JR	C4
C.SRA:	LD	C,28H
	JR	C4
C.SRL:	LD	C,38H
	JR	C4

C4:	PUSH	BC
	CALL	OPCODE
	CP	32H
	JR	Z,C4.5
	CP	40H
	JR	Z,C4.10
	CP	41H
	JR	Z,C4.20
	CP	7
	JP	NC,ERRB

	CP	6
	JR	NZ,$+3
	INC	A
C4.2:	POP	BC
	OR	C
	PUSH	AF
	LD	A,0CBH
	CALL	SETDAT
	POP	AF
	JP	SETD1
C4.5:	LD	A,6
	JR	C4.2

C4.10:	POP	BC
	LD	A,C
	OR	06H
	PUSH	AF
	LD	A,0DDH
C4.15:	CALL	SETDAT
	LD	A,0CBH
	CALL	SETDAT
	LD	HL,(VAL1)
	LD	A,L
	CALL	SETDAT
	POP	AF
	JP	SETD1
C4.20:	POP	BC
	LD	A,C
	OR	06H
	PUSH	AF
	LD	A,0FDH
	JR	C4.15

SETD2:	PUSH	HL
	LD	A,L
	CALL	SETDAT
	POP	HL
	LD	A,H
	JP	SETDAT

SETD1:	CALL	SETDAT
	JP	MAIN20

;-----------------------
